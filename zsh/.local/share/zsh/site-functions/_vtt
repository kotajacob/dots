#compdef vtu vts vtd

_vtt_avaliable_packs() {
	compadd "$@" -- ${${${(f)"$(jq -r '.categories[].packs[].name' "${VTT_CACHE:-$HOME/.cache}/${VTT_REMOTE:-vanillatweaks.net}"/rpcategories-${VTT_MCVER:-1.18}.json | sort)"}#??? }%%-[^- ]# *}
}

_vtd() {
	_arguments : \
		-f'[download multiple packs at once, similar to downloading packs from the website]:files:_files' \
		-h'[print help]' \
		-i'[when downloading multiple packs, download them individually]' \
		-n'[do not replace any files in downloaded packs]' \
		-o'[specify directory to place downloaded packs]:files:_files' \
		'*:package:_vtt_avaliable_packs'
}

_vtu() {
	_arguments : \
		-c'[clear all rpcategories files]' \
		-h'[print help]'
}

_vts() {
	_arguments : \
		-a'[list all avaliable packs]' \
		-h'[print help]' \
		'*:package:_vtt_avaliable_packs'
}

_vtt() {
	case "$service" in
		vtd) _vtd "$@";;
		vtu) _vtu "$@";;
		vts) _vts "$@";;
	esac
}

_vtt "$@"
