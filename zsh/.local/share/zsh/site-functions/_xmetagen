#compdef xir xmeta xqm xrmm xim

_xbps

_xmetagen_avaliable_packages() {
	compadd "$@" -- ${${${(f)"$(xbps-query --ignore-conf-repos --repository="${XMETA_REPODIR:-${XDG_DATA_HOME:-$HOME/.local/share}/xmeta}" -Rs '')"}#??? }%%-[^- ]# *}
}

_xir() {
	_arguments : \
		-m'[add message for installation]:message' \
		'*:package:_xbps_all_packages'
}

_xim() {
	_arguments : \
		'*:package:_xmetagen_avaliable_packages'
}

_xmeta() {
	_arguments : \
		-d'[set description of metapackage]' \
		-v'[specify version for meta package]' \
		'*:package:_xbps_all_packages'
}

_xqm() {
	_arguments : \
		'*:package:_xmetagen_avaliable_packages'
}

_xrmm() {
	_arguments : \
		'*:package:_xmetagen_avaliable_packages'
}

_xmetagen() {
	case "$service" in
		xir) _xir "$@";;
		xmeta) _xmeta "$@";;
		xqm) _xqm "$@";;
		xrmm) _xrmm "$@";;
		xim) _xim "$@";;
	esac
}

_xmetagen "$@"
